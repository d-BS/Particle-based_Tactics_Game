#[compute]
#version 450

#extension GL_EXT_shader_atomic_float : enable


//to add:
//sleeping ... ?
//switch all instances of 'distance' and 'length'
// to dist^2 & len^2


//flat 128 threads.
//1024 from the tutorial didn't work for me, so I settled on this.

layout(local_size_x = 128, local_size_y = 1, local_size_z = 1) in;

//buffer for the position of each boid
layout(set = 0, binding = 0, std430) restrict buffer Position {
	vec2 data[];
} boid_pos;

//buffer for the velocity of each boid
layout(set = 0, binding = 1, std430) restrict buffer Velocity{
	vec2 data[];
} boid_vel;

//buffer for bias provided by squad
layout(set = 0, binding = 2, std430) restrict buffer SquadBias{
	vec2 data[];
} squad_bias;

//holds first / length
layout(set = 0, binding = 3, std430) restrict buffer BinMatrix{
	int data[];
} bin_mat;

layout(set = 0, binding = 4, std430) restrict buffer BinNext{
	int data[];
} bin_next;

//parameter buffer
layout(set = 0, binding = 5, std430) restrict buffer Params{
	float num_boids;
    float image_size;
    float vision_rad;
    float avoid_rad;

	float bin_h;
	float bin_w;

	float bin_offset_x;
	float bin_offset_y;


    float alignment_factor;
    float cohesion_factor;
    float avoidance_factor;
    float damp_factor;

	
	float pass_number;

    float delta_time;
} params;


//float sleep_cutoff;
//float wake_cutoff;


//image out
//formerly rgba16f
layout(rgba32f, binding = 6) uniform image2D boid_data;


//holds health of each unit
layout(set = 0, binding = 7, std430) restrict buffer Health{
	float data[];
} health;

//holds faction # of each unit
layout(set = 0, binding = 8, std430) restrict buffer Faction{
	int data[];
} faction;

//layout(set = 0, binding = 8, std430) restrict buffer ToDelete{
//	int data[];
//} to_delete;


//function prototypes
void binning_pass();
void boid_physics_pass();
void loop_over_bin(int);
void boid_loop_interior(int);
int get_bindex();
void storeImage();



//unique identifier for this instance
int index = int(gl_GlobalInvocationID.x);


//these variables are more convenient to have here
int num_neighbors = 0;

int avoid_neighbors = 0;
vec2 avoid_direction = vec2(0,0);
vec2 avoid_velocity_ave = vec2(0,0);

vec2 average_velocity = vec2(0,0);
vec2 average_position = vec2(0,0);



void main() {
	
	
	//having this just makes some things cleaner
	if (index >= params.num_boids){
		return;
	}
	if (health.data[index] <= 0){
		
		storeImage();

		return;
	}



	if (params.pass_number == 0.0){
		binning_pass();
	}
	else if(params.pass_number == 1.0)
		boid_physics_pass();

		

	
}

void boid_physics_pass() {



	vec2 position = boid_pos.data[index];
	vec2 velocity = boid_vel.data[index];


	//last resort catches
	//dont like that I need this
	//if (isnan(position.x) || isnan(position.y) || isinf(position.x) || isinf(position.y))
	//	position = vec2(0, 0);
	//if (isnan(velocity.x) || isnan(velocity.y) || isinf(velocity.x) || isinf(velocity.y))
	//	velocity = vec2(0, 0);




	

	//bool kicker = velocity.length() >= wakeup cutoff


	

	int bindex = get_bindex();
	int[9] neighborhood = {-1, 0, 1, int(-params.bin_w - 1), int(-params.bin_w), int(-params.bin_w + 1), int(params.bin_w - 1), int(params.bin_w), int(params.bin_w + 1)};

	for(int i = 0; i < 9; i++){

		loop_over_bin(neighborhood[i] + bindex);
	}




	
	if (num_neighbors > 0){

		//this causes groups to go faster whn alignment_factor is positive
		velocity += (average_velocity / num_neighbors) * params.alignment_factor * params.delta_time;

		//applies average position
		velocity += (average_position / num_neighbors - position) * params.cohesion_factor * params.delta_time;
	}



	//Formerly
		//velocity += squad_bias * delta * bias_factor
		//magic number .075 for bias_factor
		//velocity += squad_bias.data[index] * params.delta_time * .075;
	

	vec2 personal_squad_bias = squad_bias.data[index];
	if(!isinf(personal_squad_bias[0])){
		
		vec2 bias = personal_squad_bias - position;

		//ridiculous degrees of magic numbers
		if(bias != vec2(0,0))
			velocity += normalize(bias) * 1000 * params.delta_time * .075;

	}

	//applies damping
	velocity /= 1 + params.damp_factor * params.delta_time;


	//if velocity < sleep cutoff and avg_vel < sleep cutoff
	//sleep


	//skips work if no collisions
	if(avoid_neighbors != 0){

		avoid_velocity_ave /= avoid_neighbors;
		avoid_direction /= avoid_neighbors;

		//makes avoid dir stronger the closer the boids are together
		//magic nums formerly 1.25, 2
		//avoid_direction = -avoid_direction * 2.5 + normalize(avoid_direction) * params.avoid_rad * 3;


		//takes the weighted average of current velocity and colliding velocities
		//adds avoidance vector, for final semi-elastic collision
		//make this tunable - boinginess?
		float self_v_weight = .1;
		velocity = (avoid_velocity_ave * self_v_weight + velocity) / (1 + self_v_weight) + (avoid_direction * params.avoidance_factor);
		

		//velocity = avoid_velocity_ave + (avoid_direction * params.avoidance_factor);
	}


	

	position += velocity * params.delta_time;
	

	boid_vel.data[index] = velocity;
	boid_pos.data[index] = position;


	storeImage();
}






void boid_loop_interior(int i){

	if(i!=index){

		vec2 b_pos = boid_pos.data[i];
		vec2 b_vel = boid_vel.data[i];
		vec2 position = boid_pos.data[index];

		float distance = distance(position, b_pos);

		if(distance < params.vision_rad){


			num_neighbors++;

			if(distance <= params.avoid_rad){
				avoid_neighbors++;

				vec2 to_add = position - b_pos;


				//make this into a tunable variable?
				//DANGER pls try to remove normalize
				avoid_direction += -to_add * 9.6 + normalize(to_add) * params.avoid_rad * 10;


				//avoid_direction += position - b_pos;
				avoid_velocity_ave += b_vel;
				

				if(faction.data[index] % 2 != faction.data[i] % 2)
					atomicAdd(health.data[i], -10 * params.delta_time);
				//else
				//	atomicAdd(health.data[i], -.5 * params.delta_time);
				//atomicExchange(health.data[i], 0);
				//health.data[index] = 0;

			}

			average_velocity += b_vel;
			average_position += b_pos;

		}

	}

}


void loop_over_bin(int bindex){

	if(bindex < 0 || bindex >= params.bin_h * params.bin_w)
		return;

	int to_check = bin_mat.data[bindex];

	while(to_check != -1){

		//kinda wish I had a better solution than this
		if (to_check >= params.num_boids || health.data[to_check] <=0) {
        	to_check = bin_next.data[to_check];
        	continue;
    	}

		boid_loop_interior(to_check);
		to_check = bin_next.data[to_check];
	}
}








void binning_pass() {

	
	//first, find location relative to bins
	//find bin
	//add to bin

	

	int bindex = get_bindex();

	if(bindex == -1)
		return;

	if (index < params.num_boids || health.data[index] > 0) {

		int old_head = atomicExchange(bin_mat.data[bindex], index);
		bin_next.data[index] = old_head;
	}
	return;

}



int get_bindex(){


	vec2 position = boid_pos.data[index];
	vec2 bin_pos = (position - vec2(params.bin_offset_x, params.bin_offset_y)) / params.vision_rad;


	int bx = int(floor(bin_pos.x));
    int by = int(floor(bin_pos.y));

	if (bx < 0 || bx >= params.bin_w) return -1;
    if (by < 0 || by >= params.bin_h) return -1;

	return int(bx + (by * params.bin_w));

}

void storeImage(){


	int img_size_int = int(params.image_size);
	ivec2 pixel_pos = ivec2(index % img_size_int, index / img_size_int);
	vec2 position = boid_pos.data[index];
	
	imageStore(boid_data, pixel_pos, vec4(position.x, position.y, health.data[index], 0));
}